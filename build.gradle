buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    }
}

apply plugin: 'idea'

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'com.google.protobuf'

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

group = 'ru.hack2016.microbot'
version = '0.0.11'

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'
    }
}

def generatedSourceRoot = "$projectDir/build/generated/source/proto/main/java"

sourceSets {
    main {
        java {
            srcDir generatedSourceRoot
        }
    }
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.0.0-alpha-3'
    compile 'org.apache.httpcomponents:httpclient:4.5.1'
    compile 'io.reactivex:rxjava:1.0.17'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.codehaus.groovy:groovy-all:2.4.5'

    testCompile 'junit:junit:4.12'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId group
            artifactId project.name
            version version

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = System.properties['BINTRAY_USER']
    key = System.properties['BINTRAY_KEY']
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'alfahack2016-speechkit'
    }
}

idea.module.excludeDirs = []
idea.module.sourceDirs += file(generatedSourceRoot)
idea.module.jdkName = '1.8'